# Counter-Strike in SCP: Secret Laboratory
ROUND LOCK
DISABLE RESPAWNS
STOP *

# Pre-round setup
LOCAL {TCLASS} {PLAYERS}/2

SPAWNRULE ChaosConscript {MATH-ROUND:TCLASS:DOWN}
SPAWNRULE NtfSpecialist

# AudioBot setup
COMMAND /audio add 1
WAIT SEC 1
COMMAND /audio volume 1 10
WAIT SEC 1
COMMAND /audio channel 1 Intercom
WAIT SEC 1
HINT * 5 <b><color=green>Counter-Strike: Secret Lab</color> is Ready!

WAIT UNTIL {ROUND:STARTED} = TRUE
EFFECT ADD * Ensnared 1

TPDOOR {MTF} Scp096
TPDOOR {CI} GateA
DOOR LOCK NukeSurface
DOOR LOCK SurfaceGate
DOOR OPEN GateA
DOOR OPEN Scp096

ELEVATOR LOCK LczA
ELEVATOR LOCK LczB
ELEVATOR LOCK GateA
ELEVATOR LOCK GateB
ELEVATOR LOCK Scp049

DECONTAMINATE DISABLE

COUNTDOWN * 20 <size=60><br>Game Begin

# Terrorist Message
HINT {CI} 20 <b><Size=50>You are a <color=red>Terrorist<size=27></color><br>Arm & detonate the Warhead<br>Eliminate the <color=lightblue>Counter-Terrorists

# Counter-Terrorist Message
HINT {MTF} 20 <b><Size=50>You are a <color=lightblue>Counter-Terrorist<size=27></color><br>Prevent the Warhead's detonation<br>Eliminate all the <color=red>Terrorists

WAIT SEC 19
COMMAND /audio play 1 /home/container/.config/EXILED/Configs/Sounds/OkLetsGoo.ogg
WAIT SEC 1
EFFECT REMOVE * Ensnared

COUNTDOWN * 600 <size=60><br>Counter-Strike: Secret Lab

WinCheck:
WAIT MIL 300
GOTO TerroristWinNoBomb $IF {MTF} <= 0
GOTO CTWinNoBomb $IF {CI} <= 0
GOTO BombEnding $IF {WARHEAD:ISARMED} = TRUE
GOTO WinCheck

TerroristWinNoBomb:
ROUND UNLOCK
COMMAND /audio play 1 /home/container/.config/EXILED/Configs/Sounds/T_win.ogg
STOP

CTWinNoBomb:
ROUND UNLOCK
COMMAND /audio play 1 /home/container/.config/EXILED/Configs/Sounds/CT_Win_NoBomb.ogg
STOP

BombEnding:
FASTCALL CSS_Bomb
COMMAND /audio play 1 /home/container/.config/EXILED/Configs/Sounds/BombPlant.ogg

WAIT SEC 3
HINT {MTF} 10 <color=red><b><size=50>The Bomb has been Planted</color><br><size=25>You have <color=red>45 seconds</color> to defuse the bomb<br><size=15><i><color=lightblue>(Flip the Warhead Switch)
HINT {CI} 10 <color=green><b><size=50>You have planted the Bomb</color><br><size=25>You have <color=lightblue>45 seconds to defend the bomb</color><br><size=17>Eliminate the remaining Counter-Terrorists

WAIT SEC 25
HINT {MTF} 10 <color=red><b><size=50>! 25 SECONDS REMAINING !</color><br><size=25><color=red>DISARM THE BOMB IMMEDIATELY</color><br><size=15><i><color=yellow>(Flip the Warhead Switch)

WAIT SEC 20
CASSIE SILENT .G5 yield_01 .G4 yield_01 .G3 yield_01 .G2 yield_01 .G1|<color=yellow>...
WAIT UNTIL {CASSIESPEAKING} = FALSE
GODMODE {CI} TRUE
WARHEAD DETONATE
WAIT SEC 1
GODMODE {CI} FALSE
STOP
